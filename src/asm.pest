
asm = { SOI ~ stmt* ~ EOI }

stmt = { label | inst | lit }

label = ${ lbl_name ~ ":" }

inst = ${ ident ~ (space ~ arg)* ~ eol}
    space = _{ " " | "\t" }

arg = ${ lit | arg_lbl }
    arg_lbl = ${ "<" ~ lbl_name ~ ">"}

lit = ${ lit_ref | lit_deref | str | chr | lbl | num }
    lit_ref = ${ "&" ~ lit }
    lit_deref = ${ "*" ~ (lit_deref | derefable) }
    derefable = ${ lbl | lit_ref }

lbl = ${ "'" ~ lbl_name }
lbl_name = @{ "."? ~ ident }

num = @{ "-"? ~ number }
chr = @{ "'" ~ "\\"? ~ ANY ~ "'"}
str = @{ "\"" ~ (("\\" ~ ANY) | (!"\"" ~ ANY))* ~ "\"" }

ident = @{ ident_letter ~ (ident_letter | ASCII_DIGIT)* }

number = @{ ASCII_DIGIT+ }

ident_letter = _{ ASCII_ALPHA | "_" }
eol = _{ (" " | "\t")* ~ (COMMENT | NEWLINE | EOI) }

WHITESPACE = _{ " " | "\t" | NEWLINE }
COMMENT = _{ ";" ~ (!NEWLINE ~ ANY)* }
